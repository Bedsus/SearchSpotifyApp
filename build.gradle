// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.5.0"
    ext.lifecycle_version = "2.3.1"
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

subprojects {
    def subprojectName = it.name // remember, “it” refers to the sub-project.
    if (subprojectName == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion 30
        buildToolsVersion "30.0.2"

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 30
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            debug {
                buildConfigField "String", "SERVER_URL", '"https://api.spotify.com/v1/"'
            }
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }

        buildFeatures {
            viewBinding true
        }
    }
    dependencies {
        if (subprojectName != 'core') {
            implementation project(':core')
        }

        if (subprojectName == 'app') {
            // spotify
            implementation 'com.spotify.android:auth:1.2.4'
        }

        /** all projects */
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        // androidx
        implementation 'androidx.core:core-ktx:1.5.0'
        implementation 'androidx.appcompat:appcompat:1.3.0'
        implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
        implementation 'androidx.preference:preference:1.1.1'
        implementation 'androidx.fragment:fragment:1.3.4'
        implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
        implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
        implementation 'androidx.navigation:navigation-fragment:2.3.5'
        // material
        implementation 'com.google.android.material:material:1.3.0'

        // lifecycle
        implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
        implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
        implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
        // coroutines
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'
        implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
        testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.5'
        // koin
        implementation 'io.insert-koin:koin-android:3.0.2'
        // retrofit + okhttp
        implementation 'com.squareup.retrofit2:retrofit:2.9.0'
        implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
        implementation 'com.squareup.okhttp3:logging-interceptor:4.8.1'
        // shimmer
        implementation 'com.facebook.shimmer:shimmer:0.5.0'
        // padding
        implementation 'androidx.paging:paging-runtime:3.0.0'
        // picasso
        implementation 'com.squareup.picasso:picasso:2.71828'
        // timber
        implementation 'com.jakewharton.timber:timber:4.7.1'
        // rxbinding
        implementation 'com.jakewharton.rxbinding3:rxbinding:3.1.0'
        // test
        testImplementation 'junit:junit:4.13.2'
        androidTestImplementation 'androidx.test.ext:junit:1.1.2'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}