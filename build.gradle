// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0-alpha11'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    def subprojectName = it.name // remember, “it” refers to the sub-project.
    if (subprojectName == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion 30
        buildToolsVersion "30.0.1"

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 30
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            debug {
                buildConfigField "String", "SERVER_URL", '"https://api.spotify.com/v1/"'
            }
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
    }
    dependencies {
        if (subprojectName != 'core') {
            implementation project(':core')
        }

        /** all projects */
        implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.10"
        // spotify
        implementation 'com.spotify.android:auth:1.1.0'
        // androidx
        implementation 'androidx.core:core-ktx:1.3.1'
        implementation 'androidx.appcompat:appcompat:1.2.0'
        implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
        implementation 'androidx.preference:preference:1.1.1'
        implementation 'androidx.fragment:fragment:1.2.5'
        implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
        implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
        implementation 'androidx.navigation:navigation-fragment:2.3.0'
        // material
        implementation 'com.google.android.material:material:1.2.1'
        // lifecycle
        implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
        implementation 'android.arch.lifecycle:extensions:1.1.1'
        implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
        // coroutines
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
        implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
        testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.5'
        // koin
        implementation 'org.koin:koin-android:2.1.5'
        implementation 'org.koin:koin-androidx-scope:2.1.5'
        implementation 'org.koin:koin-androidx-viewmodel:2.1.5'
        implementation 'org.koin:koin-androidx-fragment:2.1.5'
        // retrofit + okhttp
        implementation 'com.squareup.retrofit2:retrofit:2.9.0'
        implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
        implementation 'com.squareup.okhttp3:logging-interceptor:4.8.0'
        // shimmer
        implementation 'com.facebook.shimmer:shimmer:0.5.0'
        // padding
        implementation 'androidx.paging:paging-runtime:2.1.2'
        // picasso
        implementation 'com.squareup.picasso:picasso:2.71828'
        // timber
        implementation 'com.jakewharton.timber:timber:4.7.1'
        // test
        testImplementation 'junit:junit:4.13'
        androidTestImplementation 'androidx.test.ext:junit:1.1.2'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}